extends Control

const Campaign = preload("res://config/campaign_levels.gd")
var campaign_progress_path: String = Campaign.SAVE_PATH
var campaign_instructions: Label
var campaign_hint: Label
var campaign_panel: VBoxContainer
var campaign_next_button: Button
var campaign_hint_button: Button

# === UI references ===
# all the workspace pieces live here: editor, output, controls, and level viewport
@onready var editor: CodeEdit = $RootMargin/MainColumn/WorkspaceSplit/EditorOutputSplit/EditorSection/EditorPanel/EditorMargin/Editor
@onready var game_subviewport: SubViewport = $RootMargin/MainColumn/WorkspaceSplit/GameViewPanel/GameView/SubViewport
@onready var output_box: RichTextLabel = $RootMargin/MainColumn/WorkspaceSplit/EditorOutputSplit/OutputSection/OutputPanel/OutputMargin/Output
@onready var status_label: Label = $RootMargin/MainColumn/TopBarPanel/TopBar/StatusLabel
@onready var run_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/LeftButtons/RunButton
@onready var prev_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/LeftButtons/PrevButton
@onready var step_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/LeftButtons/StepButton
@onready var reset_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/LeftButtons/ResetButton
@onready var rotate_left_btn: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/RightButtons/LeftRotateButton
@onready var rotate_right_btn: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/RightButtons/RightRotateButton
@onready var grid_2d_button: Button = $SettingsOverlay/SettingsCard/SettingsContent/ViewRow/Grid2DButton

# Settings overlay
@onready var settings_overlay: Control = $SettingsOverlay
@onready var settings_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/RightButtons/SettingsButton
@onready var speed_slider: HSlider = $SettingsOverlay/SettingsCard/SettingsContent/SpeedRow/SpeedSlider
@onready var speed_value_label: Label = $SettingsOverlay/SettingsCard/SettingsContent/SpeedRow/SpeedValueLabel
@onready var settings_font_slider: HSlider = $SettingsOverlay/SettingsCard/SettingsContent/FontRow/FontSlider
@onready var settings_font_label: Label = $SettingsOverlay/SettingsCard/SettingsContent/FontRow/FontValueLabel
@onready var language_selector: OptionButton = $SettingsOverlay/SettingsCard/SettingsContent/LanguageRow/LanguageSelector
@onready var menu_button: Button = $SettingsOverlay/SettingsCard/SettingsContent/SettingsMenuButton
@onready var menu_confirm_overlay: Control = $MenuConfirmOverlay
@onready var menu_confirm_leave: Button = $MenuConfirmOverlay/ConfirmCard/ConfirmContent/ConfirmButtons/ConfirmLeaveButton
@onready var menu_confirm_stay: Button = $MenuConfirmOverlay/ConfirmCard/ConfirmContent/ConfirmButtons/ConfirmStayButton

# Compass HUD - Index order matches player.gd DIRS
@onready var compass: TextureRect = $Compass
const COMPASS_TEXTURES: Array[Texture2D] = [
	preload("res://assets/images/compass_east.png"),	# 0 = east
	preload("res://assets/images/compass_south.png"),	# 1 = south
	preload("res://assets/images/compass_west.png"),	# 2 = west
	preload("res://assets/images/compass_north.png"),	# 3 = north
]

# Popups -------------------------------------------+
# Lose overlay
@onready var lose_overlay: Control = $LoseOverlay
@onready var lose_message: Label = $LoseOverlay/LoseCard/LoseContent/LoseMessage
@onready var lose_retry_button: Button = $LoseOverlay/LoseCard/LoseContent/LoseButtons/LoseRetryButton
@onready var lose_menu_button: Button = $LoseOverlay/LoseCard/LoseContent/LoseButtons/LoseMenuButton
@onready var lose_save_button: Button = $LoseOverlay/LoseCard/LoseContent/LoseButtons/LoseSaveButton

# Win overlay
@onready var win_overlay: Control = $WinOverlay
@onready var win_retry_button: Button = $WinOverlay/WinCard/WinContent/WinButtons/WinRetryButton
@onready var win_menu_button: Button = $WinOverlay/WinCard/WinContent/WinButtons/WinMenuButton
@onready var win_save_button: Button = $WinOverlay/WinCard/WinContent/WinButtons/WinSaveButton
@onready var report_folder_dialog: FileDialog = $ReportFolderDialog

# Done overlay (for levels without a win condition)
@onready var done_overlay: Control = $DoneOverlay
@onready var done_retry_button: Button = $DoneOverlay/DoneCard/DoneContent/DoneButtons/DoneRetryButton
@onready var done_menu_button: Button = $DoneOverlay/DoneCard/DoneContent/DoneButtons/DoneMenuButton
@onready var done_save_button: Button = $DoneOverlay/DoneCard/DoneContent/DoneButtons/DoneSaveButton


# Library overlay
@onready var library_overlay: Control = $LibraryOverlay
@onready var library_button: Button = $RootMargin/MainColumn/TopBarPanel/TopBar/RightButtons/LibraryButton

# === execution components ===
# these turn student code into command output the game can actually use
const Paths = preload("res://execution/shared/paths.gd")

var validator = preload(Paths.CPP_VALIDATOR).new()
var generator = preload(Paths.CPP_GENERATOR).new()
var compiler = preload(Paths.CPP_DRIVER).new()
var py_pipeline = preload(Paths.PYTHON_PIPELINE).new()
var _commands = preload(Paths.ROBOT_COMMANDS).new()

# === language state ===
enum Language { CPP, PYTHON }
var current_language: Language = Language.CPP

# === level bootstrap ===
# this screen now loads the level definition and instantiates the playable level scene directly
var level_definition = preload(Paths.MAP_LOADER).new()
var level_scene_resource = preload(Paths.MAP_VIEW_SCENE)
var current_level_definition: Dictionary = {}

# cached runtime refs so this screen can hand commands to the live player
var game_instance: Node = null
var player_node: Node = null

# 2D flat grid view toggle
var flat_grid_node: Node2D = null
var _is_2d_mode: bool = false

# === IPC state ===
var _ipc_server = null
var _subprocess_pid: int = -1
var _ipc_active: bool = false
var _ipc_loop_running: bool = false

# === step history for step back ===
var _cmd_history: Array = []	# [{cmd, src_line, snap}] - snap is state before the cmd ran
var _step_index: int = 0		# how many commands the user has seen
var _in_replay: bool = false	# true when replaying from history

# === pause state ===
var _paused: bool = false
signal _resume

var current_line_offset: int = 0
var _is_handling_lose: bool = false

var global_level_name := ""
var exec_speed: float = 0.5
var _run_outcome: String = "incomplete"  # "win" | "lose" | "incomplete" | "move_limit"
var _run_had_error: bool = false  # set when the subprocess emits [ERROR]
const MOVE_LIMIT := 999
const CMD_LIMIT := 9999
const PROGRAM_TIMEOUT_SECONDS := 10.0
const ALLOWED_IPC_COMMANDS := ["MOVE", "TURN_LEFT", "PICK_OBJECT", "PUT_OBJECT"]
const ALLOWED_IPC_QUERIES := [
	"FRONT_IS_CLEAR", 
	"RIGHT_IS_CLEAR", 
	"LEFT_IS_CLEAR",
	"WALL_IN_FRONT", 
	"WALL_ON_RIGHT", 
	"WALL_ON_LEFT",
	"AT_GOAL", 
	"OBJECT_HERE", 
	"CARRIES_OBJECT",
	"IS_FACING_NORTH", 
	"IS_FACING_EAST", 
	"IS_FACING_WEST",
]
const MAX_STUDENT_OUTPUT_LINES := 200
const MAX_STUDENT_SOURCE_CHARS := 100000
var _student_output_lines := 0

func _ready() -> void:
	# Kill any subprocess left over from a previous session that was force-closed.
	# auto_accept_quit disabled so _notification can clean up before Godot exits.
	get_tree().set_auto_accept_quit(false)		# required for _notification to intercept window close
	if OS.get_name() == "Windows":
		OS.execute("taskkill", ["/F", "/IM", "student_program.exe"], [], true)
	else:
		OS.execute("pkill", ["-f", "student_program"], [], true)

	_set_status("Ready", "")
	editor.text = "#include \"robot.hpp\"\n\nint main() {\n    move();\n}\n"
	editor.grab_focus()

	_setup_editor()
	_setup_syntax_highlighting()
	_setup_language_selector()
	_setup_campaign_ui()

	# compass updates whenever the player's logical facing changes
	EventManager.player_facing_changed.connect(_on_player_facing_changed)
	
	run_button.text = "▶ Run"
	run_button.pressed.connect(_on_run_button_pressed)
	step_button.pressed.connect(_on_step_button_pressed)
	prev_button.pressed.connect(_on_prev_button_pressed)
	prev_button.disabled = true
	reset_button.pressed.connect(_on_reset_button_pressed)
	lose_retry_button.pressed.connect(_on_lose_retry)
	win_retry_button.pressed.connect(_on_win_retry)
	lose_menu_button.pressed.connect(_on_go_to_menu)
	win_menu_button.pressed.connect(_on_go_to_menu)
	win_save_button.pressed.connect(_on_save_report)
	lose_save_button.pressed.connect(_on_save_report)
	done_retry_button.pressed.connect(_on_done_retry)
	done_menu_button.pressed.connect(_on_go_to_menu)
	done_save_button.pressed.connect(_on_save_report)

	report_folder_dialog.hide()
	if not report_folder_dialog.dir_selected.is_connected(_on_report_folder_selected):
		report_folder_dialog.dir_selected.connect(_on_report_folder_selected)

	if rotate_left_btn != null and not rotate_left_btn.pressed.is_connected(l_rotate_button_up):
		rotate_left_btn.pressed.connect(l_rotate_button_up)

	if rotate_right_btn != null and not rotate_right_btn.pressed.is_connected(r_rotate_button_up):
		rotate_right_btn.pressed.connect(r_rotate_button_up)

	menu_button.pressed.connect(_on_main_menu_button_pressed)
	menu_confirm_leave.pressed.connect(_on_go_to_menu)
	menu_confirm_stay.pressed.connect(func():
		menu_confirm_overlay.hide()
		_set_controls_disabled(false))
	speed_slider.value_changed.connect(_on_speed_change)
	settings_font_slider.value_changed.connect(_on_font_size_changed)
	settings_button.pressed.connect(_on_settings_button_pressed)

	grid_2d_button.pressed.connect(_on_grid_2d_button_pressed)

	settings_overlay.visible = false
	menu_confirm_overlay.visible = false
	library_overlay.visible = false

	await get_tree().process_frame
	_load_level_scene(true)


func _setup_campaign_ui() -> void:
	campaign_panel = VBoxContainer.new()
	campaign_panel.add_theme_constant_override("separation", 6)
	var column := $RootMargin/MainColumn
	column.add_child(campaign_panel)
	column.move_child(campaign_panel, 1)
	campaign_instructions = Label.new()
	campaign_instructions.autowrap_mode = TextServer.AUTOWRAP_WORD_SMART
	campaign_instructions.add_theme_font_size_override("font_size", 20)
	campaign_panel.add_child(campaign_instructions)
	campaign_hint_button = Button.new()
	campaign_hint_button.text = "Show Hint"
	campaign_hint_button.size_flags_horizontal = Control.SIZE_SHRINK_BEGIN
	campaign_hint_button.pressed.connect(func():
		campaign_hint.visible = not campaign_hint.visible
		campaign_hint_button.text = "Hide Hint" if campaign_hint.visible else "Show Hint")
	campaign_panel.add_child(campaign_hint_button)
	campaign_hint = Label.new()
	campaign_hint.autowrap_mode = TextServer.AUTOWRAP_WORD_SMART
	campaign_hint.add_theme_font_size_override("font_size", 18)
	campaign_hint.add_theme_color_override("font_color", Color("8ed6ef"))
	campaign_panel.add_child(campaign_hint)
	campaign_next_button = Button.new()
	campaign_next_button.text = "Next Level"
	campaign_next_button.custom_minimum_size.y = 36
	campaign_next_button.pressed.connect(_on_win_next)
	var buttons := $WinOverlay/WinCard/WinContent/WinButtons
	buttons.add_child(campaign_next_button)
	buttons.move_child(campaign_next_button, 0)
	campaign_next_button.hide()


func _refresh_campaign_instructions() -> void:
	var index := Campaign.index_for_path(SelectedLevel.path)
	campaign_panel.visible = index >= 0
	campaign_hint.hide()
	campaign_hint_button.text = "Show Hint"
	if index < 0:
		return
	var description := PackedStringArray(current_level_definition.get("description", []))
	campaign_instructions.text = "Level %d / %d - %s\n%s" % [index + 1, Campaign.LEVELS.size(), Campaign.LEVELS[index].title, " ".join(description)]
	campaign_hint.text = current_level_definition.get("hint", "")


func _load_level_scene(load_editor_text: bool = true, preserve_camera: bool = false) -> void:
	var saved_camera_state: Dictionary = {}
	if preserve_camera and game_instance and game_instance.has_method("get_camera_state"):
		saved_camera_state = game_instance.get_camera_state()

	# clear out any existing level scene from the viewport
	for child in game_subviewport.get_children():
		child.queue_free()
	flat_grid_node = null

	# create and attach the playable level scene
	game_instance = level_scene_resource.instantiate()
	game_subviewport.add_child(game_instance)

	if preserve_camera and not saved_camera_state.is_empty() \
			and game_instance.has_method("set_pending_camera_restore"):
		game_instance.set_pending_camera_restore(saved_camera_state)

	# grab the player node so runtime systems can control it later
	if not game_instance.has_node("WorldRoot/Player"):
		push_error("Level scene is missing node path: WorldRoot/Player")
		return

	player_node = game_instance.get_node("WorldRoot/Player")
	if player_node.has_signal("lose_triggered") and not player_node.lose_triggered.is_connected(_on_player_lose):
		player_node.lose_triggered.connect(_on_player_lose)
	if game_instance.has_signal("level_complete") and not game_instance.level_complete.is_connected(_on_level_complete):
		game_instance.level_complete.connect(_on_level_complete)
	if game_instance.has_signal("level_incomplete") and not game_instance.level_incomplete.is_connected(_trigger_incomplete_lose):
		game_instance.level_incomplete.connect(_trigger_incomplete_lose)

	# === LOAD PATH ===
	var level_path := ""


	if SelectedLevel.path.strip_edges() != "":
		level_path = SelectedLevel.path
	else:
		push_error("No level path available.")
		log_error("NO LEVEL PATH FOUND")
		return

	if not FileAccess.file_exists(level_path):
		push_error("Level file does not exist: " + level_path)
		log_error("FILE NOT FOUND")
		return

	global_level_name = level_path
	var raw: Dictionary = level_definition.load(level_path)

	if not raw.ok:
		push_error("Level load failed: %s" % raw.error)
		log_error("LOAD FAILED: " + raw.error)
		return

	current_level_definition = raw.definition
	_refresh_campaign_instructions()

	# preload starter code into editor only when requested
	if load_editor_text:
		_load_editor_template_for_current_language()

	# build level
	if game_instance.has_method("build_level"):
		game_instance.build_level(raw.definition)

	# (re)create the 2D flat grid view alongside the isometric scene
	var FlatGridScript = load("res://map/scripts/flat_grid_view.gd")
	flat_grid_node = FlatGridScript.new()
	game_subviewport.add_child(flat_grid_node)
	flat_grid_node.setup(game_instance, player_node)
	flat_grid_node.visible = _is_2d_mode
	if _is_2d_mode:
		game_instance.camera.enabled = false
		flat_grid_node.activate()
	else:
		game_instance.camera.enabled = true
		game_instance.camera.make_current()


func _load_editor_template_for_current_language() -> void:
	if current_level_definition.is_empty():
		_load_default_editor_template()
		return

	if not current_level_definition.has("editor"):
		_load_default_editor_template()
		return

	var editor_data = current_level_definition["editor"]

	# === OLD FORMAT SUPPORT ===
	if editor_data is String:
		editor.text = editor_data
		return

	if editor_data is Array:
		var old_lines: PackedStringArray = []
		for line in editor_data:
			old_lines.append(str(line))
		editor.text = "\n".join(old_lines)
		return

	# === NEW FORMAT SUPPORT ===
	if editor_data is Dictionary:
		var key := "cpp" if current_language == Language.CPP else "python"

		if editor_data.has(key):
			var starter = editor_data[key]

			if starter is String:
				editor.text = starter
				return

			if starter is Array:
				var lines: PackedStringArray = []
				for line in starter:
					lines.append(str(line))
				editor.text = "\n".join(lines)
				return

	_load_default_editor_template()


func _load_default_editor_template() -> void:
	if current_language == Language.CPP:
		editor.text = "#include \"robot.hpp\"\n\nint main()\n{\n\tmove();\n\n\treturn 0;\n}\n"
	else:
		editor.text = "from robot import *\n\nmove()\n"


# === editor setup ===
func _setup_editor() -> void:
	editor.highlight_current_line = true
	editor.draw_control_chars = false
	editor.indent_automatic = true
	editor.indent_use_spaces = true
	editor.indent_size = 4


# === language selector ===
func _setup_language_selector() -> void:
	language_selector.clear()
	language_selector.add_item("C++")
	language_selector.add_item("Python")
	language_selector.select(0)

	if not language_selector.item_selected.is_connected(_on_language_changed):
		language_selector.item_selected.connect(_on_language_changed)


func _on_language_changed(index: int) -> void:
	_stop_execution()
	current_language = Language.CPP if index == 0 else Language.PYTHON
	_cmd_history.clear()
	_step_index = 0
	_in_replay = false
	output_box.clear()
	_clear_editor_highlights()

	run_button.text = "▶ Run"
	run_button.disabled = false
	step_button.disabled = false
	prev_button.disabled = true
	reset_button.disabled = false
	rotate_left_btn.disabled = false
	rotate_right_btn.disabled = false

	if current_language == Language.CPP:
		_setup_syntax_highlighting()
		_load_editor_template_for_current_language()
		_set_status("Ready", "")
	elif current_language == Language.PYTHON:
		_setup_python_highlighting()
		_load_editor_template_for_current_language()
		_set_status("Ready", "")
	
	_load_level_scene(false, true)


# === syntax highlighting ===

func _setup_syntax_highlighting() -> void:
	var highlighter := CodeHighlighter.new()

	var keywords := [
		"int", "double", "float", "bool", "char", "void",
		"if", "else", "while", "for", "return",
		"true", "false", "break", "continue"
	]
	for word in keywords:
		highlighter.add_keyword_color(word, Color(0.40, 0.70, 1.00))

	for command in _commands.COMMANDS:
		highlighter.add_keyword_color(command.name, Color(0.80, 0.60, 1.00))

	for sensor in _commands.SENSORS:
		highlighter.add_keyword_color(sensor.name, Color(0.60, 0.90, 1.00))

	highlighter.number_color = Color(0.95, 0.65, 0.30)
	highlighter.symbol_color = Color(0.85, 0.85, 0.85)
	highlighter.function_color = Color(0.95, 0.85, 0.45)
	highlighter.member_variable_color = Color(0.85, 0.85, 0.85)
	highlighter.add_color_region("\"", "\"", Color(0.60, 0.90, 0.60), false)
	highlighter.add_color_region("'", "'", Color(0.60, 0.90, 0.60), false)
	highlighter.add_color_region("//", "", Color(0.50, 0.50, 0.50), true)
	highlighter.add_color_region("/*", "*/", Color(0.50, 0.50, 0.50), false)
	highlighter.add_keyword_color("include", Color(0.95, 0.45, 0.75))
	highlighter.add_color_region("<", ">", Color(1.0, 0.6, 0.25), true)

	editor.syntax_highlighter = highlighter


func _setup_python_highlighting() -> void:
	var highlighter := CodeHighlighter.new()

	var keywords := [
		"def", "if", "elif", "else", "while", "for", "in",
		"return", "True", "False", "None", "and", "or", "not",
		"pass", "break", "continue"
	]
	for word in keywords:
		highlighter.add_keyword_color(word, Color(0.40, 0.70, 1.00))

	for command in _commands.COMMANDS:
		highlighter.add_keyword_color(command.name, Color(0.80, 0.60, 1.00))

	for sensor in _commands.SENSORS:
		highlighter.add_keyword_color(sensor.name, Color(0.60, 0.90, 1.00))

	highlighter.add_keyword_color("from", Color(0.95, 0.45, 0.75))
	highlighter.add_keyword_color("import", Color(0.95, 0.45, 0.75))
	highlighter.add_color_region("*", "", Color(0.95, 0.45, 0.75), true)
	highlighter.number_color = Color(0.95, 0.65, 0.30)
	highlighter.symbol_color = Color(0.85, 0.85, 0.85)
	highlighter.function_color = Color(0.95, 0.85, 0.45)
	highlighter.add_color_region("\"", "\"", Color(0.60, 0.90, 0.60), false)
	highlighter.add_color_region("'", "'", Color(0.60, 0.90, 0.60), false)
	highlighter.add_color_region("#", "", Color(0.50, 0.50, 0.50), true)
	highlighter.add_color_region("\"\"\"", "\"\"\"", Color(0.60, 0.90, 0.60), false)

	editor.syntax_highlighter = highlighter


# === button handlers ===

func _on_run_button_pressed() -> void:
	if _ipc_active:
		if _paused:
			if _step_index < _cmd_history.size():
				await _resume_after_step_back()
			else:
				_paused = false
				run_button.text = "❚❚ Pause"
				_set_status("Running...", "")
				_resume.emit()
		else:
			_paused = true
			run_button.text = "▶ Resume"
			_set_status("Paused", "")
		return

	_paused = false
	_run_pipeline()


# The subprocess is held at the original pause position; the world is at the stepped-back position. 
# Replay cached commands at the user's selected speed to catch the world up,
# so it looks like seamless continuation, then unblock the IPC loop.
# Pause check between commands so the user can interrupt the catch-up.
func _resume_after_step_back() -> void:
	_in_replay = false
	_paused = false
	run_button.text = "❚❚ Pause"
	prev_button.disabled = true
	step_button.disabled = true
	_set_status("Running...", "")

	while _step_index < _cmd_history.size() and _ipc_active:
		var entry = _cmd_history[_step_index]
		_step_index += 1
		await _execute_cmd(entry.cmd, entry.src_line)
		if not _ipc_active:
			return
		if _paused:
			# User pressed Pause mid-replay. Stop catching up.
			# If there are still cached commands left, stay in replay mode so
			# Step continues via _replay_step_forward; otherwise drop to live ste
			_in_replay = _step_index < _cmd_history.size()
			run_button.text = "▶ Resume"
			_set_status("Paused", "")
			step_button.disabled = false
			prev_button.disabled = _step_index <= 0
			return
	
	_resume.emit()


func _on_step_button_pressed() -> void:
	# Running: pause first
	if _ipc_active and not _paused:
		_paused = true
		run_button.text = "▶ Resume"
		_set_status("Paused", "")
		return

	# Paused: single step forward
	if _ipc_active and _paused:
		step_button.disabled = true
		prev_button.disabled = true
		if _in_replay:
			await _replay_step_forward()
		else:
			_resume.emit()	# loop executes one cmd then re-pauses
		return

	# start a fresh run, but begin paused so the first command is a single step
	_paused = true
	_run_pipeline()


func _on_prev_button_pressed() -> void:
	if not _ipc_active or _step_index <= 0 or not _paused:
		return

	_step_index -= 1
	_in_replay = true
	_restore_snapshot(_cmd_history[_step_index].snap)
	if flat_grid_node != null and is_instance_valid(flat_grid_node):
		# Build path: every snapshot up to and including the restored one is a
		# (gx, gy, facing) point the player has actually stood at, oldest first
		var positions: Array = []
		for i in range(_step_index + 1):
			var s = _cmd_history[i].snap
			positions.append({gx = s.grid_x, gy = s.grid_y, facing = s.facing})
		flat_grid_node.rebuild_trail(positions)
	step_button.disabled = false
	prev_button.disabled = _step_index <= 0


func _take_snapshot() -> Dictionary:
	var obj_copy: Dictionary = {}

	for key in game_instance.object_data:
		obj_copy[key] = game_instance.object_data[key].duplicate()

	return {
		grid_x = player_node.grid_x,
		grid_y = player_node.grid_y,
		facing = player_node.facing,
		carried_object = player_node.carried_object,
		object_data = obj_copy
	}


func _restore_snapshot(snap: Dictionary) -> void:
	if player_node == null or game_instance == null:
		return

	player_node.grid_x = snap.grid_x
	player_node.grid_y = snap.grid_y
	player_node.facing = snap.facing
	player_node.carried_object = snap.carried_object
	player_node.position = game_instance.player_grid_position(snap.grid_x, snap.grid_y)

	player_node.update_animation(false)
	game_instance.restore_object_data(snap.object_data)

	if _step_index > 0:
		_highlight_editor_line(_cmd_history[_step_index - 1].src_line)
	else:
		_clear_editor_highlights()


func _replay_step_forward() -> void:
	var entry = _cmd_history[_step_index]
	_step_index += 1
	await _execute_cmd(entry.cmd, entry.src_line)
	if not _ipc_active:
		return

	log_line("✓ %s" % entry.cmd.to_lower())
	if _step_index >= _cmd_history.size():
		# Caught up to live position... next step will be a real live step via _resume.
		_in_replay = false

	_set_status("Paused", "")
	step_button.disabled = false
	prev_button.disabled = _step_index <= 0


func _highlight_editor_line(line: int) -> void:
	_clear_editor_highlights()

	var adjusted := line - 1
	if current_language == Language.CPP:
		adjusted = line - current_line_offset - 1

	if adjusted >= 0 and adjusted < editor.get_line_count():
		editor.set_line_background_color(adjusted, Color(0.30, 0.60, 0.30, 0.25))
		editor.set_caret_line(adjusted)
		editor.center_viewport_to_caret()


func _clear_editor_highlights() -> void:
	for i in range(editor.get_line_count()):
		editor.set_line_background_color(i, Color(0, 0, 0, 0))


func _on_reset_button_pressed() -> void:
	_stop_execution()
	_clear_editor_highlights()
	_cmd_history.clear()
	_step_index = 0
	_in_replay = false
	run_button.text = "▶ Run"
	run_button.disabled = false
	step_button.disabled = false
	reset_button.disabled = false
	rotate_left_btn.disabled = false
	rotate_right_btn.disabled = false
	prev_button.disabled = true

	output_box.clear()
	log_header("reset")
	log_line("Level reloaded.")
	_set_status("Ready", "")
	_load_level_scene(false, true)

func _on_speed_change(value: float) -> void:
	exec_speed = value
	speed_value_label.text = "%.2fs" % value


func _on_font_size_changed(value: float) -> void:
	var font_size := int(value)
	editor.add_theme_font_size_override("font_size", font_size)
	output_box.add_theme_font_size_override("normal_font_size", font_size)
	settings_font_label.text = "%dpt" % font_size


func _on_settings_button_pressed() -> void:
	settings_overlay.visible = not settings_overlay.visible
	if settings_overlay.visible:
		library_overlay.visible = false


# === funny lose messages ===
const LOSE_MESSAGES := [
	"Your player has left the chat.",
	"Have you...tried turning it off and on again?",
	"Your player took an unscheduled vacation.",
	"Your player says: \"I quit.\"",
	"404: Success not found.",
	"Instructions unclear. Your player now in another dimension.",
	"Your player walked into a wall. Impressive dedication!",
	"Your player has filed a complaint with HR.",
	"Maybe try fewer walls next time?",
	"Your player called in sick.",
	"The matrix has rejected your code.",
	"Skill issue detected.",
	"Your player tripped over their own code.",
	"Your player is on strike. Have you tried negotiating?",
	"Splat. Your player is now wall decor.",
]

func _set_controls_disabled(disabled: bool) -> void:
	run_button.disabled = disabled
	prev_button.disabled = disabled
	step_button.disabled = disabled
	reset_button.disabled = disabled
	language_selector.disabled = disabled
	editor.editable = not disabled
	rotate_left_btn.disabled = disabled
	rotate_right_btn.disabled = disabled
	settings_button.disabled = disabled
	library_button.disabled = disabled
	if disabled:
		settings_overlay.hide()
		library_overlay.hide()


func _get_funny_lose_message() -> String:
	return LOSE_MESSAGES[randi() % LOSE_MESSAGES.size()]


func _trigger_move_limit_lose() -> void:
	if _is_handling_lose:
		return
	_is_handling_lose = true
	_run_outcome = "move_limit"
	_stop_execution()

	log_header("lose")
	log_error("Move Limit Reached")
	_set_status("You lost", "error")

	lose_message.text = _get_funny_lose_message()
	lose_overlay.visible = true
	_set_controls_disabled(true)


# Shared handler for both "incomplete" lose conditions:
#   - Player code finished without winning or crashing
#   - player landed on the goal tile but objectives weren't satisfied
func _trigger_incomplete_lose(reason: String) -> void:
	if _is_handling_lose:
		return
	_is_handling_lose = true
	_run_outcome = "incomplete"
	_stop_execution()

	log_header("lose")
	log_error(reason)
	_set_status("You lost", "error")

	lose_message.text = _get_funny_lose_message()
	lose_overlay.visible = true
	_set_controls_disabled(true)


func _on_player_lose(reason: String) -> void:
	if _is_handling_lose:
		return
	_is_handling_lose = true
	_run_outcome = "lose"
	_stop_execution()

	log_header("lose")
	log_error(reason)
	_set_status("You lost", "error")

	lose_message.text = _get_funny_lose_message()
	lose_overlay.visible = true
	_set_controls_disabled(true)


func _on_level_complete() -> void:
	_run_outcome = "win"
	_stop_execution()
	var campaign_index := Campaign.index_for_path(SelectedLevel.path)
	if campaign_index >= 0:
		var save_error := Campaign.mark_complete(SelectedLevel.path, campaign_progress_path)
		if save_error != OK:
			log_error("Could not save campaign progress. Replay this level to try again.")
		campaign_next_button.visible = save_error == OK and campaign_index + 1 < Campaign.LEVELS.size()
		if campaign_index == Campaign.LEVELS.size() - 1:
			$WinOverlay/WinCard/WinContent/WinTitle.text = "Campaign Complete!"
			$WinOverlay/WinCard/WinContent/WinMessage.text = "You completed all three levels. Well done!"

	log_header("level complete")
	log_success("Your robot reached the goal!")
	_set_status("Level Complete!", "ok")

	win_overlay.visible = true
	_set_controls_disabled(true)


func _on_lose_retry() -> void:
	lose_overlay.visible = false
	_is_handling_lose = false
	_set_controls_disabled(false)
	_on_reset_button_pressed()


func _on_win_retry() -> void:
	win_overlay.visible = false
	_set_controls_disabled(false)
	_on_reset_button_pressed()


func _on_win_next() -> void:
	var next_index := Campaign.index_for_path(SelectedLevel.path) + 1
	if next_index <= 0 or not Campaign.is_unlocked(next_index, Campaign.completed_ids(campaign_progress_path)):
		return
	_stop_execution()
	SelectedLevel.path = Campaign.LEVELS[next_index].path
	win_overlay.visible = false
	_is_handling_lose = false
	_set_controls_disabled(false)
	_on_reset_button_pressed()
	_load_editor_template_for_current_language()
	$WinOverlay/WinCard/WinContent/WinTitle.text = "Level Complete!"
	$WinOverlay/WinCard/WinContent/WinMessage.text = "Your robot reached the goal!"
	campaign_next_button.hide()
	editor.grab_focus()


func _on_go_to_menu() -> void:
	get_tree().change_scene_to_file("res://main_menu/scenes/main_menu.tscn")


# === pipeline execution ===

func _stop_execution() -> void:
	_paused = false
	_ipc_active = false
	_ipc_loop_running = false

	if _ipc_server != null:
		_ipc_server.stop()
		_ipc_server = null
	if _subprocess_pid != -1:
		OS.kill(_subprocess_pid)
		_subprocess_pid = -1

	# If IPC loop is paused waiting for a step, unblock it so it can exit cleanly
	_resume.emit()

func _enforce_program_timeout(pid: int) -> void:
	await get_tree().create_timer(PROGRAM_TIMEOUT_SECONDS).timeout

	if _ipc_active and _subprocess_pid == pid:
		log_error("Program stopped: it ran longer than %d seconds." % PROGRAM_TIMEOUT_SECONDS)
		_set_status("Timed out", "error")
		_stop_execution()
		_re_enable_buttons()

func _run_pipeline() -> void:
	# Capture caller's intent BEFORE _stop_execution wipes _paused.
	var start_paused: bool = _paused
	_stop_execution()
	_student_output_lines = 0

	if editor.text.length() > MAX_STUDENT_SOURCE_CHARS:
		log_error("Source code is too large. Maximum size is 100,000 characters.")
		_set_status("Validation failed", "error")
		_re_enable_buttons()
		return

	reset_button.disabled = false
	run_button.disabled = true
	step_button.disabled = true
	rotate_left_btn.disabled = true
	rotate_right_btn.disabled = true

	_set_status("Compiling...", "")
	output_box.clear()
	log_header("run")
	_cmd_history.clear()
	_step_index = 0
	_in_replay = false
	_run_outcome = "incomplete"
	_run_had_error = false
	await get_tree().process_frame

	# Python path
	if current_language == Language.PYTHON:
		var v: Dictionary = py_pipeline.validate(editor.text)
		if not v.ok:
			for err in v.errors:
				log_error("line %d: %s" % [err.line, err.message])
			_set_status("Validation failed", "error")
			_re_enable_buttons()
			return
	else:
		var v: Dictionary = validator.validate(editor.text)
		if not v.ok:
			for err in v.errors:
				log_error("line %d: %s" % [err.line, err.message])
			_set_status("Validation failed", "error")
			_re_enable_buttons()
			return

	# start IPC server
	var IPCServer = preload(Paths.IPC_SERVER)
	_ipc_server = IPCServer.new()
	var crypto := Crypto.new()
	var ipc_token := crypto.generate_random_bytes(32).hex_encode()

	if not _ipc_server.start(ipc_token):
		log_error("Could not open a local TCP port for IPC. Is the port range 27015-27115 blocked?")
		_set_status("IPC failed", "error")
		_re_enable_buttons()
		return

	# C++ path
	if current_language == Language.CPP:
		var generated: Dictionary = generator.generate(editor.text)
		current_line_offset = generated.line_offset
		compiler.prepare_build_files(generated.generated_source, _ipc_server.port, ipc_token)

		var build: Dictionary = compiler.compile_program()
		if not build.ok:
			log_error(compiler.remap_diagnostics(build.output, generated.line_offset))
			_set_status("Compile failed", "error")
			_ipc_server.stop()
			_ipc_server = null
			_re_enable_buttons()
			return
		_set_status("Compiled - launching...", "")
		_subprocess_pid = compiler.start_program()
	else:
		current_line_offset = 0
		_subprocess_pid = py_pipeline.start(editor.text, _ipc_server.port, ipc_token)

	if _subprocess_pid == -1:
		log_error("Failed to launch subprocess.")
		_set_status("Launch failed", "error")
		_ipc_server.stop()
		_ipc_server = null
		_re_enable_buttons()
		return

	_enforce_program_timeout(_subprocess_pid)

	_set_status("Running...", "")
	if not await _ipc_server.wait_for_connection(get_tree()):
		log_error("Subprocess did not connect within 5 seconds.")
		_set_status("Timeout", "error")
		_stop_execution()
		_re_enable_buttons()
		return

	_ipc_active = true
	_paused = start_paused
	log_header("executing")

	run_button.disabled = false
	if _paused:
		run_button.text = "▶ Resume"
		_set_status("Paused", "")
	else:
		run_button.text = "❚❚ Pause"
	await _run_ipc_loop()


func _run_ipc_loop() -> void:
	while _ipc_active:
		var line: String = await _ipc_server.read_line(get_tree())

		if not _ipc_active:
			break

		if line == "[CANCELLED]" or line == "[DISCONNECT]":
			break

		if line == "[AUTH_FAILED]" or line == "[PROTOCOL_ERROR]":
			log_error("Security error: student program failed IPC authentication.")
			_run_had_error = true
			_set_status("Security error", "error")
			_stop_execution()
			_re_enable_buttons()
			return
		if line.begins_with("[CMD]"):
			var cmd := line.trim_prefix("[CMD] ")
			var src_line := -1
			if " [LINE] " in cmd:
				var parts := cmd.split(" [LINE] ")
				cmd = parts[0].strip_edges()
				src_line = int(parts[1].strip_edges())

			if not ALLOWED_IPC_COMMANDS.has(cmd):
				log_error("Security error: unrecognized robot command.")
				_run_had_error = true
				_set_status("Security error", "error")
				_stop_execution()
				_re_enable_buttons()
				return 

			if _cmd_history.size() >= CMD_LIMIT:
				_trigger_move_limit_lose()
				break

			var snap = _take_snapshot()
			_cmd_history.append({cmd = cmd, src_line = src_line, snap = snap})
			_step_index = _cmd_history.size()

			await _execute_cmd(cmd, src_line)

			if not _ipc_active:
				break

			if _paused:
				log_line("✓ %s" % cmd.to_lower())
				_set_status("Paused", "")
				step_button.disabled = false
				prev_button.disabled = false
				await _resume
				if not _ipc_active:
					break
			_ipc_server.send("OK")

		elif line.begins_with("[QUERY]"):
			var query := line.trim_prefix("[QUERY] ")
			if " [LINE] " in query:
				query = query.split(" [LINE] ")[0].strip_edges()

			if not ALLOWED_IPC_QUERIES.has(query):
				log_error("Security error: unrecognized robot query.")
				_run_had_error = true
				_set_status("Security error", "error")
				_stop_execution()
				_re_enable_buttons()
				return 
	
			var answer := _answer_query(query)
			_ipc_server.send(answer)

		elif line.begins_with("[PRINT]"):
			if _student_output_lines < MAX_STUDENT_OUTPUT_LINES:
				log_line(line.trim_prefix("[PRINT] "))
			elif _student_output_lines == MAX_STUDENT_OUTPUT_LINES:
				log_warning("Output limit reached; further student output is hidden.")

			_student_output_lines += 1

		elif line.begins_with("[ERROR]"):
			log_error(line.trim_prefix("[ERROR] "))
			_run_had_error = true

		elif line == "[DONE]":
			break

	_ipc_loop_running = false
	if _ipc_active:
		# Loop exited naturally (subprocess sent [DONE] or disconnected) without a
		# win or crash.
		if _run_outcome == "incomplete" and not _is_handling_lose and not _run_had_error:
			if _level_has_win_condition():
				_trigger_incomplete_lose("Did not reach the goal.")
			else:
				_on_execution_done()
				return
		else:
			_stop_execution()

			run_button.text = "▶ Run"
			run_button.disabled = true
			step_button.disabled = true
			if _run_had_error:
				_set_status("Error", "error")
			else:
				_set_status("Done", "ok")


func _on_execution_done() -> void:
	_run_outcome = "done"
	_stop_execution()
	run_button.text = "▶ Run"
	run_button.disabled = true
	step_button.disabled = true
	_set_status("Done", "ok")
	done_overlay.visible = true
	_set_controls_disabled(true)


func _level_has_win_condition() -> bool:
	if not current_level_definition.has("goal"):
		return false
	var goal = current_level_definition["goal"]
	if goal is Dictionary and goal.is_empty():
		return false
	return true


func _on_done_retry() -> void:
	done_overlay.visible = false
	_set_controls_disabled(false)
	_on_reset_button_pressed()


# Intercepts window close so the subprocess is killed before Godot exits
func _notification(what: int) -> void:
	if what == NOTIFICATION_WM_CLOSE_REQUEST:
		_stop_execution()
		get_tree().quit()

func _execute_cmd(cmd: String, src_line: int) -> void:
	_highlight_editor_line(src_line)
	if not _paused:
		log_line("▶ %s" % cmd.to_lower())
	if player_node == null:
		return
	match cmd:
		"MOVE":
			var moves_so_far := 0
			for i in range(min(_step_index, _cmd_history.size())):
				if _cmd_history[i].cmd == "MOVE":
					moves_so_far += 1
			if moves_so_far > MOVE_LIMIT:
				_trigger_move_limit_lose()
				return
			await player_node.move_forward(exec_speed)
		"TURN_LEFT":
			player_node.turn_left()
			await get_tree().create_timer(exec_speed * 0.2).timeout
		"TURN_RIGHT":
			player_node.turn_right()
			await get_tree().create_timer(exec_speed * 0.2).timeout
		"PICK_OBJECT":
			player_node.pick_object()
		"PUT_OBJECT":
			player_node.put_object()


func _answer_query(query: String) -> String:
	if player_node == null or game_instance == null:
		return "false"
	var facing: String = player_node.facing
	match query:
		"FRONT_IS_CLEAR":
			return _bool(_is_clear(facing))
		"RIGHT_IS_CLEAR":
			return _bool(_is_clear(_right_of(facing)))
		"LEFT_IS_CLEAR":
			return _bool(_is_clear(_left_of(facing)))
		"WALL_IN_FRONT":
			return _bool(not _is_clear(facing))
		"WALL_ON_RIGHT":
			return _bool(not _is_clear(_right_of(facing)))
		"WALL_ON_LEFT":
			return _bool(not _is_clear(_left_of(facing)))
		"IS_FACING_NORTH":
			return _bool(facing == "north")
		"IS_FACING_EAST":
			return _bool(facing == "east")
		"IS_FACING_WEST":
			return _bool(facing == "west")
		"AT_GOAL":
			return _bool(game_instance.is_at_goal(player_node.grid_x, player_node.grid_y))
		"OBJECT_HERE":
			return _bool(game_instance.tile_has_any_object(player_node.grid_x, player_node.grid_y))
		"CARRIES_OBJECT":
			return _bool(player_node.carried_object != "")
	return "false"


func _bool(value: bool) -> String:
	return "true" if value else "false"


func _is_clear(dir: String) -> bool:
	var gx: int = player_node.grid_x
	var gy: int = player_node.grid_y
	var next := _next_pos(gx, gy, dir)
	var wall_blocked: bool = game_instance.is_move_blocked(gx, gy, dir)
	var in_bounds: bool = game_instance.is_in_bounds(next.x, next.y)
	return not wall_blocked and in_bounds


func _next_pos(gx: int, gy: int, dir: String) -> Vector2i:
	match dir:
		"north":
			return Vector2i(gx, gy + 1)
		"south":
			return Vector2i(gx, gy - 1)
		"east":
			return Vector2i(gx + 1, gy)
		"west":
			return Vector2i(gx - 1, gy)
	return Vector2i(gx, gy)


func _right_of(facing: String) -> String:
	match facing:
		"north":
			return "east"
		"east":
			return "south"
		"south":
			return "west"
		"west":
			return "north"
	return facing


func _left_of(facing: String) -> String:
	match facing:
		"north":
			return "west"
		"west":
			return "south"
		"south":
			return "east"
		"east":
			return "north"
	return facing


func _set_status(text: String, state: String) -> void:
	status_label.text = text
	match state:
		"ok":
			status_label.add_theme_color_override("font_color", Color(0.47, 0.87, 0.58))
		"error":
			status_label.add_theme_color_override("font_color", Color(0.88, 0.47, 0.47))
		_:
			status_label.add_theme_color_override("font_color", Color(0.72, 0.76, 0.81))


func _re_enable_buttons() -> void:
	_paused = false
	run_button.text = "▶ Run"
	run_button.disabled = false
	step_button.disabled = false
	prev_button.disabled = true
	reset_button.disabled = false
	rotate_left_btn.visible = not _is_2d_mode
	rotate_right_btn.visible = not _is_2d_mode


func _on_grid_2d_button_pressed() -> void:
	_is_2d_mode = not _is_2d_mode

	if flat_grid_node != null and is_instance_valid(flat_grid_node):
		flat_grid_node.visible = _is_2d_mode

	if game_instance != null and is_instance_valid(game_instance):
		game_instance.camera.enabled = not _is_2d_mode
		game_instance.visible = not _is_2d_mode
		if not _is_2d_mode:
			game_instance.camera.make_current()

	if _is_2d_mode and flat_grid_node != null and is_instance_valid(flat_grid_node):
		flat_grid_node.activate()
	elif not _is_2d_mode and flat_grid_node != null and is_instance_valid(flat_grid_node):
		flat_grid_node.deactivate()

	rotate_left_btn.visible = not _is_2d_mode
	rotate_right_btn.visible = not _is_2d_mode
	grid_2d_button.text = "3D View" if _is_2d_mode else "2D View"


func _on_save_report() -> void:
	report_folder_dialog.popup_centered_ratio(0.75)


func _on_report_folder_selected(folder: String) -> void:
	var level_name := global_level_name.get_file().get_basename().replace(" ", "_")
	var base_name := "%s_CompletionReport" % level_name
	var dir_name := base_name
	var counter := 1
	while DirAccess.dir_exists_absolute(folder.path_join(dir_name)):
		dir_name = "%s_%d" % [base_name, counter]
		counter += 1
	var dir_path := folder.path_join(dir_name)
	DirAccess.make_dir_recursive_absolute(dir_path)

	# save text report
	var report := _build_report()
	var txt_path := dir_path.path_join("%s_report.txt" % level_name)
	var file := FileAccess.open(txt_path, FileAccess.WRITE)
	if file:
		file.store_string(report)
		file.close()
	else:
		log_error("Could not save report to: " + txt_path)
		return

	# capture screenshot (switch to 2D view momentarily if needed)
	var was_2d := _is_2d_mode
	if not was_2d and flat_grid_node != null:
		game_instance.visible = false
		game_instance.camera.enabled = false
		flat_grid_node.visible = true
		flat_grid_node.activate()
	await get_tree().process_frame
	await get_tree().process_frame
	var screenshot := game_subviewport.get_texture().get_image()
	if not was_2d and flat_grid_node != null:
		flat_grid_node.deactivate()
		flat_grid_node.visible = false
		game_instance.visible = true
		game_instance.camera.enabled = true
		game_instance.camera.make_current()

	var png_path := dir_path.path_join("%s_screenshot.png" % level_name)
	screenshot.save_png(png_path)

	log_success("Report saved to: " + dir_path)


# === logging ===

func log_line(text: String) -> void:
	output_box.append_text(text + "\n")


func log_header(title: String) -> void:
	output_box.append_text("[color=#5b8dd9]── %s ──[/color]\n" % title.to_upper())


func log_success(text: String) -> void:
	output_box.append_text("[color=#78d897]✓[/color]  %s\n" % text)


func log_warning(text: String) -> void:
	output_box.append_text("[color=#e5b567]⚠[/color]  %s\n" % text)


func log_error(text: String) -> void:
	output_box.append_text("[color=#e17777]✗[/color]  %s\n" % text)


func l_rotate_button_up() -> void:
	EventManager.rotate_camera_right.emit()


func r_rotate_button_up() -> void:
	EventManager.rotate_camera_left.emit()

# toggle library_overlay between visible and invisible when library_button is pressed
func _on_library_button_pressed() -> void:
	library_overlay.visible = not library_overlay.visible

# switches overlays invisible when clicking outside them
func _input(event) -> void:
	if event is InputEventMouseButton and event.pressed and event.button_index == MOUSE_BUTTON_LEFT:
		if library_overlay.visible:
			var clicked_overlay = library_overlay.get_global_rect().has_point(event.position)
			var clicked_button = library_button.get_global_rect().has_point(event.position)
			if not clicked_overlay and not clicked_button:
				library_overlay.hide()
		if settings_overlay.visible:
			var clicked_overlay = settings_overlay.get_global_rect().has_point(event.position)
			var clicked_button = settings_button.get_global_rect().has_point(event.position)
			if not clicked_overlay and not clicked_button:
				settings_overlay.hide()

func _on_main_menu_button_pressed() -> void:
	settings_overlay.hide()
	_set_controls_disabled(true)
	menu_confirm_overlay.visible = true

func _build_report() -> String:
	var cols := int(current_level_definition.get("cols", 0))
	var rows := int(current_level_definition.get("rows", 0))
	var walls: Dictionary = current_level_definition.get("walls", {})

	var start_x := 1
	var start_y := 1
	var start_facing := "north"
	var robots: Array = current_level_definition.get("robots", [])
	if not robots.is_empty():
		var r: Dictionary = robots[0]
		start_x = int(r.get("x", 1))
		start_y = int(r.get("y", 1))
		var ori := int(r.get("_orientation", 3))
		var dirs := ["east", "south", "west", "north"]
		if ori >= 0 and ori < dirs.size():
			start_facing = dirs[ori]

	var end_x := start_x
	var end_y := start_y
	var end_facing := start_facing
	if player_node != null:
		end_x = player_node.grid_x
		end_y = player_node.grid_y
		end_facing = player_node.facing

	var cmd_letters: Array = []
	var move_count := 0
	for entry in _cmd_history:
		var letter := _cmd_to_report_letter(entry.cmd)
		if letter == "":
			continue
		cmd_letters.append(letter)
		if entry.cmd == "MOVE":
			move_count += 1
	cmd_letters.append(_run_outcome_marker())

	var deposit_zones: Dictionary = current_level_definition.get("goal", {}).get("objects", {})
	var start_obj_data: Dictionary = current_level_definition.get("objects", {})
	var end_obj_data: Dictionary = {}
	var end_carried := ""
	if game_instance != null and is_instance_valid(game_instance):
		end_obj_data = game_instance.object_data
	if player_node != null:
		end_carried = player_node.carried_object

	var level_name := global_level_name.get_file().get_basename()
	var lang_name := "C++" if current_language == Language.CPP else "Python"
	var date := Time.get_date_string_from_system()

	var report := "=== CODE & CONQUER - LEVEL REPORT ===\n"
	report += "Level:    %s\n" % level_name
	report += "Language: %s\n" % lang_name
	report += "Date:     %s\n" % date
	report += "\n"
	report += "Starting position: (%d, %d) - %s\n" % [start_x, start_y, start_facing]
	report += "Starting world state:\n\n"
	report += _render_world_text(cols, rows, walls, start_x, start_y, start_facing, start_obj_data, deposit_zones)
	report += "\nCarrying: nothing\n"
	report += "\n"
	report += "Ending position: (%d, %d) - %s\n" % [end_x, end_y, end_facing]
	report += "Ending world state:\n\n"
	report += _render_world_text(cols, rows, walls, end_x, end_y, end_facing, end_obj_data, deposit_zones)
	report += "\nCarrying: %s\n" % (end_carried if end_carried != "" else "nothing")
	report += "\n\n"
	report += "Sequence: " + "".join(cmd_letters) + "\n"
	report += "Move Count: %d\n" % move_count
	report += "======================================\n"
	report += "KEY\n"
	report += "======================================\n"
	report += "\n"
	report += "SEQUENCE LETTERS\n"
	report += "  M  move forward\n"
	report += "  T  turn left\n"
	report += "  U  pick up object\n"
	report += "  D  put down object\n"
	report += "\n"
	report += "SEQUENCE OUTCOME\n"
	report += "  !  win\n"
	report += "  ?  lose (collision or did not reach goal)\n"
	report += "  .  finished — no win condition on this level\n"
	report += "  #  command/move limit hit (%d moves)\n" % MOVE_LIMIT
	report += "\n"
	report += "OBJECT LETTERS\n"
	report += "  A  apple\n"
	report += "  B  banana\n"
	report += "  C  carrot\n"
	report += "  S  star\n"
	report += "  K  token\n"
	report += "\n"
	report += "GRID CELLS  (each cell = one tile, letter + count)\n"
	report += "  (Xn)  deposit zone needs n of X — not yet satisfied\n"
	report += "  [Xn]  deposit zone needs n of X — satisfied\n"
	report += "   Xn   n of object X on the floor\n"
	report += "   .    empty tile\n"
	report += "  ^v<>  robot facing north / south / west / east\n"
	return report


func _cmd_to_report_letter(cmd: String) -> String:
	match cmd:
		"MOVE":
			return "M"
		"TURN_LEFT":
			return "T"
		"PICK_OBJECT":
			return "U"
		"PUT_OBJECT":
			return "D"
	return ""


func _run_outcome_marker() -> String:
	match _run_outcome:
		"win":
			return "!"
		"lose", "incomplete":
			return "?"
		"done":
			return "."
		"move_limit":
			return "#"
		_:
			return "?"


func _facing_arrow(facing: String) -> String:
	match facing:
		"north":
			return "^"
		"south":
			return "v"
		"east":
			return ">"
		"west":
			return "<"
	return "?"


func _wall_at(walls: Dictionary, x: int, y: int, dir: String) -> bool:
	var key := "%d,%d" % [x, y]
	if not walls.has(key):
		return false
	for d in walls[key]:
		if str(d).to_lower() == dir:
			return true
	return false


# Sparse-wall ASCII grid. Outer edges always drawn, '+' at every corner,
# interior walls only where they exist.
func _object_letter(obj_name: String) -> String:
	match obj_name:
		"apple":  return "A"
		"banana": return "B"
		"carrot": return "C"
		"star":   return "S"
		"token":  return "K"
	return "?"


func _render_world_text(cols: int, rows: int, walls: Dictionary, px: int, py: int, facing: String, obj_data: Dictionary = {}, deposit_zones: Dictionary = {}) -> String:
	if cols <= 0 or rows <= 0:
		return ""

	var lines: Array = []

	var outer := "   +"
	for c in range(cols):
		outer += "----+"
	lines.append(outer)

	for row in range(rows, 0, -1):
		var row_line := "%2d |" % row
		for col in range(1, cols + 1):
			var key := "%d,%d" % [col, row]
			var glyph: String
			if col == px and row == py:
				glyph = " %s  " % _facing_arrow(facing)
			elif deposit_zones.has(key) and typeof(deposit_zones[key]) == TYPE_DICTIONARY and not deposit_zones[key].is_empty():
				var obj_name: String = deposit_zones[key].keys()[0]
				var required: int = int(deposit_zones[key][obj_name])
				var n := str(required) if required <= 9 else "+"
				var have := 0
				if obj_data.has(key) and typeof(obj_data[key]) == TYPE_DICTIONARY:
					have = int((obj_data[key] as Dictionary).get(obj_name, 0))
				if have >= required:
					glyph = "[%s%s]" % [_object_letter(obj_name), n]
				else:
					glyph = "(%s%s)" % [_object_letter(obj_name), n]
			elif obj_data.has(key) and typeof(obj_data[key]) == TYPE_DICTIONARY and not obj_data[key].is_empty():
				var obj_name: String = obj_data[key].keys()[0]
				var count: int = int(obj_data[key][obj_name])
				var n := str(count) if count <= 9 else "+"
				glyph = " %s%s " % [_object_letter(obj_name), n]
			else:
				glyph = " .  "
			row_line += glyph
			if col < cols:
				row_line += "|" if _wall_at(walls, col, row, "east") else " "
		row_line += "|"
		lines.append(row_line)

		if row > 1:
			var sep := "   +"
			for col in range(1, cols + 1):
				sep += "----+" if _wall_at(walls, col, row - 1, "north") else "    +"
			lines.append(sep)

	lines.append(outer)

	var label := "    "
	for col in range(1, cols + 1):
		var c := str(col)
		var pad_l := (4 - c.length()) / 2
		label += " ".repeat(pad_l) + c + " ".repeat(4 - c.length() - pad_l)
		if col < cols:
			label += " "
	lines.append(label)

	return "\n".join(lines)



func _on_player_facing_changed(facing_index: int) -> void:
	compass.texture = COMPASS_TEXTURES[facing_index]